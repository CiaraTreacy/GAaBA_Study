options
  fullscreen
  scale 
  resolution 1000 800
  frame 1000 800

bitmaps
  mask
  instructions1
  instructions2
  instructions3
  go_signal
  nogo_signal
  mistake_wrong_press
  mistake_missed
  ready1
  ready2
  ready3

task 11 #go
  keys space
  set $errorstatus 0
  show bitmap go_signal
  readkey 1 2000 # wait 2 seconds for key to be pressed
  clear 1
  show bitmap mask
  if STATUS == TIMEOUT
    set $errorstatus 1
    show bitmap mistake_missed
    delay 2000
    clear 2
  fi
  delay 500 # intertrial interval
  save BLOCKNAME TASKNAME RT $errorstatus

task 00 #nogo
  keys space
  set $errorstatus 0
  show bitmap nogo_signal
  readkey 1 2000
  clear 1
  show bitmap mask
  if STATUS != TIMEOUT  ## there should be a TIME OUT, so if not, we have a mistake
    set $errorstatus 1
    show bitmap mistake_wrong_press
    delay 2000
    clear 2
  fi
  delay 500 # intertrial interval
  save BLOCKNAME TASKNAME RT $errorstatus

task 1 #go
  keys space
  set $errorstatus 0
  show bitmap go_signal
  readkey 1 2000 # wait 2 seconds for key to be pressed
  clear 1
  show bitmap mask
  if STATUS == TIMEOUT
    set $errorstatus 1
  fi
  delay 500 # intertrial interval
  save BLOCKNAME TASKNAME RT $errorstatus

task 0 #nogo
  keys space
  set $errorstatus 0
  show bitmap nogo_signal
  readkey 1 2000
  clear 1
  show bitmap mask
  if STATUS == TIMEOUT
    set $errorstatus 1
  fi
  delay 500 # intertrial interval
  save BLOCKNAME TASKNAME RT $errorstatus

# -------------------------------------

block training
  message instructions1
  message instructions2
  bitmap ready3
  delay 1000
  bitmap ready2
  delay 1000  
  bitmap ready1
  delay 1000
  tasklist
    11 20
    00 5
  end
  feedback
    text align left
    text color yellow    
    set &RT mean c3 
    text -200 -100 "Average RT in conditions:"
    text -200  -25 &RT ; postfix " ms"
    text -200  100 "Press space bar to continue"
  end

block realtest
  message instructions3
  bitmap ready3
  delay 1000
  bitmap ready2
  delay 1000  
  bitmap ready1
  delay 1000
  tasklist
    1 180
    0 45
  end
  feedback
    text align left
    text color yellow    
    set &RT mean c3
    text -200 -100 "Average RT in conditions:"
    text -200  -25 &RT ; postfix " ms"
    text -200  100 "Well done, you have completed the task"
  end
