options
  fullscreen
  scale 
  resolution 1000 800
  frame 1000 800

bitmaps
  mask
  instructions1
  instructions2
  instructions3
  go_signal
  nogo_signal
  mistake_wrong_press
  mistake_missed

task 11 #go
  keys space
  set $errorstatus 0
  show bitmap go_signal
  readkey 1 2000 # wait 2 seconds for key to be pressed
  clear 1
  show bitmap mask
  if STATUS == TIMEOUT
    set $errorstatus 1
    show bitmap mistake_missed
    delay 2000
    clear 2
  fi
  delay 500 # intertrial interval
  save BLOCKNAME TASKNAME RT $errorstatus

task 00 #nogo
  keys space
  set $errorstatus 0
  show bitmap nogo_signal
  readkey 1 2000
  clear 1
  show bitmap mask
  if STATUS != TIMEOUT  ## there should be a TIME OUT, so if not, we have a mistake
    set $errorstatus 1
    show bitmap mistake_wrong_press
    delay 2000
    clear 2
  fi
  delay 500 # intertrial interval
  save BLOCKNAME TASKNAME RT $errorstatus

task 1 #go
  keys space
  set $errorstatus 0
  show bitmap go_signal
  readkey 1 2000 # wait 2 seconds for key to be pressed
  clear 1
  show bitmap mask
  clear 2
  delay 500 # intertrial interval
  save BLOCKNAME TASKNAME RT $errorstatus

task 0 #nogo
  keys space
  set $errorstatus 0
  show bitmap nogo_signal
  readkey 1 2000
  clear 1
  show bitmap mask
  clear 2
  delay 500 # intertrial interval
  save BLOCKNAME TASKNAME RT $errorstatus

# -------------------------------------

block training
  message instructions1
  message instructions2
  tasklist
    11 20
    00 5
  end
  feedback
    set &totalGo       count    ; select c2 == 1 
    set &GoMistakes    count    ; select c2 == 1 && c4 == 1
    set &GoMistakesP   perc 20  ; select c2 == 1 && c4 == 1
    set &totalNoGo     count    ; select c2 == 0 
    set &NoGoMistakes  count    ; select c2 == 0 && c4 == 1
    set &NoGoMistakesP perc 5   ; select c2 == 0 && c4 == 1
    text align left
    text -200 -200 "Results in training block:"    
    text -200 -150 &totalGo        ; prefix "Number Go trials:" 
    text -200 -100 &GoMistakes     ; prefix "Number Go mistakes:"
    text -200 -50  &GoMistakesP    ; prefix "Go mistakes:" ; postfix "%"
    text -200   0  &totalNoGo      ; prefix "Number No Go trials:" 
    text -200  50  &NoGoMistakes   ; prefix "Number No Go mistakes:"
    text -200 100  &NoGoMistakesP  ; prefix "No Go mistakes:" ; postfix "%"
    text -200 150  "Press space bar to continue"
  end

block realtest
  message instructions3
  tasklist
    1 180
    0 45
  end
  feedback
    set &totalGo       count    ; select c2 == 1 
    set &GoMistakes    count    ; select c2 == 1 && c4 == 1
    set &GoMistakesP   perc 180 ; select c2 == 1 && c4 == 1
    set &totalNoGo     count    ; select c2 == 0 
    set &NoGoMistakes  count    ; select c2 == 0 && c4 == 1
    set &NoGoMistakesP perc 45  ; select c2 == 0 && c4 == 1
    text align left
    text -200 -200 "Results in training block:"    
    text -200 -150 &totalGo        ; prefix "Number Go trials:" 
    text -200 -100 &GoMistakes     ; prefix "Number Go mistakes:"
    text -200 -50  &GoMistakesP    ; prefix "Go mistakes:" ; postfix "%"
    text -200   0  &totalNoGo      ; prefix "Number No Go trials:" 
    text -200  50  &NoGoMistakes   ; prefix "Number No Go mistakes:"
    text -200 100  &NoGoMistakesP  ; prefix "No Go mistakes:" ; postfix "%"
    text -200 150  "Press space bar to continue"
  end  
