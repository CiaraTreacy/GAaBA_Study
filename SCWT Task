options
  fullscreen
  scale 
  resolution 1000 800
  frame 1000 800

bitmaps
  instructions1
  instructions2
  instructions3
  correct
  mistake
  fixpoint
  yellowyellow 
  yellowgreen  
  yellowblue   
  yellowred    
  redyellow    
  redgreen     
  redblue      
  redred       
  greenyellow  
  greengreen   
  greenblue    
  greenred     
  blueyellow   
  bluegreen    
  blueblue     
  bluered
  ready1
  ready2
  ready3

fonts
  arial 20

# table: write condition,stimulus,response

table stroop
  "yellow yellow 1" yellowyellow 4
  "yellow yellow 1" yellowyellow 4
  "yellow green  0" yellowgreen  2
  "yellow blue   0" yellowblue   3
  "yellow red    0" yellowred    1
  "yellow yellow 1" yellowyellow 4
  "red red       1" redred       1
  "red red       1" redred       1
  "red yellow    0" redyellow    4
  "red green     0" redgreen     2
  "red blue      0" redblue      3
  "red red       1" redred       1
  "green green   1" greengreen   2
  "green green   1" greengreen   2
  "green yellow  0" greenyellow  4
  "green blue    0" greenblue    3
  "green red     0" greenred     1
  "green green   1" greengreen   2
  "blue blue     1" blueblue     3
  "blue blue     1" blueblue     3
  "blue yellow   0" blueyellow   4
  "blue green    0" bluegreen    2
  "blue red      0" bluered      1
  "blue blue     1" blueblue     3

task strooptraining
  table stroop
  keys h j k l ## red-h (1); green-j (2); blue-k (3); yellow-l (4)
  delay 500
  show bitmap fixpoint # stimulus 1
  delay 200
  clear 1 # remove fixpoint 
  delay 100
  show bitmap @2 # stimulus 2
  readkey @3 2000
  clear 2 # remove stimulus 2
  if STATUS == CORRECT
    show bitmap correct # stimulus 3 (feedback)
  fi
  if STATUS != CORRECT  # stimulus 3 (feedback)
    show bitmap mistake
  fi
  delay 500
  clear 3 # remove feedback stimulus
  save BLOCKNAME @1 TABLEROW KEY STATUS RT
 
 task strooprealtest
  table stroop
  keys h j k l ## red-h (1); green-j (2); blue-k (3); yellow-l (4)
  delay 500
  show bitmap fixpoint # stimulus 1
  delay 200
  clear 1 # remove fixpoint 
  delay 100
  show bitmap @2 # stimulus 2
  readkey @3 2000
  clear 2 # remove stimulus 2
  save BLOCKNAME @1 TABLEROW KEY STATUS RT
  
block training 
  message instructions1
  message instructions2
  bitmap ready3
  delay 1000
  bitmap ready2
  delay 1000  
  bitmap ready1
  delay 1000
  tasklist
    strooptraining 16  # run the stroop task 16 trials.
  end
  feedback
    text align left
    text color yellow    
    set &RT mean c8 
    text -200 -100 "Average RT in conditions:"
    text -200  -25 &RT ; postfix " ms"
    text -200  100 "Press space bar to continue"
  end

block realtest
  message instructions3
  bitmap ready3
  delay 1000
  bitmap ready2
  delay 1000  
  bitmap ready1
  delay 1000
  tasklist
    strooprealtest 120  # run the stroop task 120 trials.
  end
  feedback
    text align left
    text color yellow    
    set &RT mean c8 
    text -200 -100 "Average RT in conditions:"
    text -200  -25 &RT ; postfix " ms"
    text -200  100 "Press space bar to continue"
  end
