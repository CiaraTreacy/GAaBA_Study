options
  bitmapdir stimuli
  fullscreen
  scale 
  resolution 1000 800
  frame 1000 800

bitmaps
  fix
  fixgreen
  fixred
  instructions1
  instructions2
  instructions3
  ready1
  ready2
  ready3

fonts
  Small arial 18
  Large arial 40

table flanker
 "XXXXX" 1 1
 "XXCXX" 1 1
 "XXVXX" 0 2
 "XXBXX" 0 2
 "CCXCC" 1 1
 "CCCCC" 1 1
 "CCVCC" 0 2
 "CCBCC" 0 2
 "VVXVV" 0 1
 "VVCVV" 0 1
 "VVVVV" 1 2
 "VVBVV" 1 2
 "BBXBB" 0 1
 "BBCBB" 0 1
 "BBVBB" 1 2
 "BBBBB" 1 2

task flanker
  table flanker
  keys a l
  font Large
  show bitmap fix 0 25
  show text @1 0 -25 255 255 255
  readkey @3 2000
  if STATUS == CORRECT
    clear 1 2
    show bitmap fixgreen 0 25
    delay 150
    clear 3
    delay 150
    show bitmap fixgreen 0 25
    delay 150
    clear 4
 fi
 if STATUS != CORRECT
    clear 1 2
    show bitmap fixred 0 25
    delay 300
    clear 3
    delay 300
    show bitmap fixred 0 25
    delay 300
    clear 4
 fi
 delay 500
 save BLOCKNAME @1 @2 KEY STATUS RT
 
 block training
  message instructions1
  message instructions2
  bitmap ready3
  delay 1000
  bitmap ready2
  delay 1000  
  bitmap ready1
  delay 1000
  tasklist
    flanker 30
  end
  feedback
    text align left
    text color yellow    
    set &RTCon mean c5 ; select c3 == 1 && c4 == 1 
    set &RTInc mean c5 ; select c3 == 0 && c4 == 1
    set &FlankerEffect expression &RTInc - &RTCon
    text -200 -100 "Average RT in conditions:"
    text -200  -25 &RTCon ; prefix "Compatible: "     ; postfix " ms"
    text -200   50 &RTInc ; prefix "Incompatible: "   ; postfix " ms"
    text -200  125 &FlankerEffect ; prefix "Flanker effect: " ; postfix " ms"
    text -200  200 "Press space bar to continue"
  end
 
 block realtest
  message instructions3
  bitmap ready3
  delay 1000
  bitmap ready2
  delay 1000  
  bitmap ready1
  delay 1000
  tasklist
    flanker 150
  end
  feedback
    text align left
    text color yellow    
    set &RTCon mean c5 ; select c3 == 1 && c4 == 1 
    set &RTInc mean c5 ; select c3 == 0 && c4 == 1
    set &FlankerEffect expression &RTInc - &RTCon
    text -200 -100 "Average RT in conditions:"
    text -200  -25 &RTCon ; prefix "Compatible: "     ; postfix " ms"
    text -200   50 &RTInc ; prefix "Incompatible: "   ; postfix " ms"
    text -200  125 &FlankerEffect ; prefix "Flanker effect: " ; postfix " ms"
    text -200  200 "Press space bar to continue"
  end
